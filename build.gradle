plugins {
    id 'java-library'
    id 'com.jfrog.bintray' version '1.8.4'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'

task wrapper(type: Wrapper) {
    //if you change this version you will need to run the 'gradlew wrapper' command twice from the command line to auto update the gradle cache
    gradleVersion = "4.10.2"
}

group 'com.pnuema.java'
version '1.3'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

ext {
    siteUrl = 'https://github.com/barnhill/barcode-java'
    gitUrl = 'https://github.com/barnhill/barcode-java.git'
    descr = 'Barcode image generation library in Java'
}

bintray {
    if (System.getenv("BINTRAY_USER") != null)
        user = System.getenv("BINTRAY_USER")
    else
        user = BINTRAY_USER

    if (System.getenv("BINTRAY_APIKEY") != null)
        key = System.getenv("BINTRAY_APIKEY")
    else
        key = BINTRAY_APIKEY

    publications = ['mavenJava']
    dryRun = false //[Default: false] Whether to run this as dry-run, without deploying
    publish = true //[Default: false] Whether version should be auto published after an upload
    override = false //[Default: false] Whether to override version artifacts already published
    pkg {
        configurations = ['archives']
        repo = "maven"
        name = "barcode-java"
        desc = descr
        websiteUrl = "https://github.com/barnhill/barcode-java"
        githubRepo = 'barnhill/barcode-java'
        githubReleaseNotesFile = 'README.md'
        issueTrackerUrl = 'https://github.com/barnhill/barcode-java/issues'
        vcsUrl = "https://github.com/barnhill/barcode-java.git"
        licenses = ["Apache-2.0"]
        labels = ['java', 'barcode', 'image', 'jar', 'generator']
        publicDownloadNumbers = true
        publish = true
        version {
            gpg {
                sign = true
                if (System.getenv("BINTRAY_GPGPASSWORD") != null)
                    passphrase = System.getenv("BINTRAY_GPGPASSWORD")
                else
                    passphrase = BINTRAY_GPGPASSWORD
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId project.name
            version version
            pom {
                name = project.name
                packaging = 'jar'
                description = descr
                url = siteUrl
                inceptionYear = '2018'
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "barnhill"
                        name = "Brad Barnhill"
                        email = "bradfordbarnhill@gmail.com"
                    }
                }

                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = siteUrl
                }
            }
        }
    }
}
